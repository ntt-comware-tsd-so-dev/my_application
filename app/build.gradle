apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        applicationId "com.aylanetworks.agilelink"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 57
        versionName "5.5.00-rc1"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            try {
                storeFile file(System.getenv("KEYSTORE"))
            } catch (all) {
            }
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
        debug {
            storeFile rootProject.file("release/store/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    productFlavors {
        aGoogle {
        }
        bBaiduPush {
        }
    }

    sourceSets{
        bBaiduPush {
            jniLibs.srcDirs = ['libs']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

task initialize {
    doFirst {
        println 'Doing first'
        cloneGitRepo()
    }
}

dependencies {
    bBaiduPushCompile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':websocketLibrary')
    compile project(':Android_AylaSDK')
    androidTestCompile 'com.android.support:support-annotations:25.3.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    compile project(':AMAP_Core')
    wearApp project(':wear')
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.android.support:cardview-v7:25.3.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.google.android.gms:play-services-auth:10.2.1'
    compile 'com.google.android.gms:play-services-gcm:10.2.1'
    compile 'com.google.android.gms:play-services-wearable:10.2.1'
    compile 'com.google.android.gms:play-services-location:10.2.1'
    compile 'com.google.android.gms:play-services-places:10.2.1'
    compile 'com.google.android.gms:play-services-maps:10.2.1'
    compile 'org.altbeacon:android-beacon-library:2.9.1'
    compile 'com.android.support:multidex:1.0.1'
}

android.applicationVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath()) + files(ext.androidJar)
        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
        }
        options.overview = "overview.html"
        options.setWindowTitle("Agile Link Application Framework")
        options.addStringOption("-stylesheetfile " + projectDir + "stylesheet.css")
        options.stylesheetFile = new File(projectDir, "stylesheet.css");
        exclude '**/BuildConfig.java', '**/R.java', '**/com/aylanetworks/aaml/**'
    }
}
